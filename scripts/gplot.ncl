; General-purpose plot using NCL, for use in CliMAF. 
; Usage and interfacing : see CliMAF doc http://climaf.readthedocs.org/en/latest/scripts/gplot.html

; Second version : L.Vignon - november 2015

load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"

;--------------------------------------------------------------------------------------------
undef("str_convert")
; Convert an input string into logical, integer, float or the same string
function str_convert(str:string)

local val, valf
begin
  
  if .not. ismissing(str_match(str,"True")) then
    return(True)
  else
    if .not. ismissing(str_match(str,"False")) then
      return(False)
    else

      if ismissing(str_match(str,".")) then ; integer or string
        val=stringtoint(str)
        if ismissing(val) then 
          return(str)
        else
          return(val)
        end if
      else                                  ; float (or string)
        valf=stringtofloat(str)
        if ismissing(valf) then 
          return(str)
        else
          return(valf)
        end if
      end if

    end if ; if False
  end if ; if True

end 

;--------------------------------------------------------------------------------------------

undef("res_list")
; Extract graphic resources from 'opt' associated to resource 'fct_res'
procedure res_list(opt:string, fct_res:logical)

local reslist, resval, key, value, value_convert
begin
  
  reslist=str_split(opt,"|")
  do i=0,dimsizes(reslist)-1 
    resval=str_split(reslist(i),"=")
    key=resval(0)
    value=resval(1)
    value_convert=str_convert(value)
    fct_res@$key$=value_convert
    delete(value_convert)
  end do
  
  return
end

;--------------------------------------------------------------------------------------------

undef("shading")
; Shade contour regions given low and/or high values using patterns
function shading(routname, field:numeric, resname:logical)

begin

  if ( isvar("shade_below") .or. isvar("shade_above") ) then 

    if ( isvar("shade_below") .and. (.not. isvar("shade_above")) ) then 
      resname@gsnShadeLow = 17   
      if isvar("shading_options") then res_list(shading_options, resname) end if
      routname = gsn_contour_shade(routname, shade_below, min(field), resname) 
    else 
      if ( isvar("shade_above") .and. (.not. isvar("shade_below")) ) then 
        resname@gsnShadeHigh = 17
        if isvar("shading_options") then res_list(shading_options, resname) end if
        routname = gsn_contour_shade(routname, max(field), shade_above, resname) 
      else 
        
        if ( shade_above .lt. shade_below ) then
          resname@gsnShadeMid = 17
          if isvar("shading_options") then res_list(shading_options, resname) end if
          routname = gsn_contour_shade(routname, shade_above, shade_below, resname) 
        else
          resname@gsnShadeLow = 17   
          resname@gsnShadeHigh =17
	  if isvar("shading_options") then res_list(shading_options, resname) end if
          routname = gsn_contour_shade(routname, shade_below, shade_above, resname) 
        end if

      end if
    end if 
   
    return(routname)

  end if
  
end

;--------------------------------------------------------------------------------------------

undef("data_select")
; Select value 'arg' of optional argument 'arg_name' for dimension 'dim_field_name' of 'field' 
; and create a list, called 'select_list', of extracted values 
function data_select(arg_name:string, arg:numeric, field:numeric, dim_field_name:string, select_list:string)

local VarDimNames, dim, rk, index, value
begin
  
  VarDimNames = getVarDimNames(field)  ; getting dimensions from metadata
  dim=dimsizes(field)
  rk=dimsizes(dim)

  if isint(arg) then
    print("Selecting index "+arg+" for "+arg_name+", i.e. "+arg_name+" = "+field&$dim_field_name$(arg))
    if arg_name .eq. "time" then
      ;value=cd_calendar(field&$dim_field_name$(arg),2) ; value returned will be in the format YYYYMMDD
      utc_date=cd_calendar(field&$dim_field_name$(arg),0) ; array returned will be of type float and dimensioned dimsizes(time)x6
      year  = tointeger(utc_date(:,0))    ; Convert to integer for
      month = tointeger(utc_date(:,1))    ; use sprinti 
      day   = tointeger(utc_date(:,2))
      value = sprinti("%0.4i", year) + "/" + sprinti("%0.2i", month) + "/" + sprinti("%0.2i", day) 
    else
      value=field&$dim_field_name$(arg)
    end if
    select_list=select_list+" "+arg_name+"="+value
    if rk .eq. 4 then
      new_fld=field(arg,:,:,:)     
    else 
      if rk .eq. 3 then
        new_fld=field(arg,:,:)
      else 
        if rk .eq. 2 then
          new_fld=field(arg,:)
        end if ;rk eq 2
      end if ; rk eq 3
    end if ; rk eq 4
    
  else
    if isfloat(arg) then 
      ; To convert date to a UT-referenced date => 'time' would be given in the format 'YYYYMM.': to be continued.  
      ;utc_time=cd_calendar(field&$VarDimNames(0),1)
      ;index=closest_val(time,utc_time)
      ;print("Selecting time close to "+time+", i.e. time = "+utc_time(index))
      index=closest_val(arg,field&$dim_field_name$)
      print("Selecting "+arg_name+" close to "+arg+", i.e. "+arg_name+" = "+field&$dim_field_name$(index))
      if arg_name .eq. "time" then
        ;value=cd_calendar(field&$dim_field_name$(index),2) ;value returned will be in the format YYYYMMDD
        utc_date=cd_calendar(field&$dim_field_name$(index),0) ; array returned will be of type float and dimensioned dimsizes(time)x6
        year  = tointeger(utc_date(:,0))    ; Convert to integer for
        month = tointeger(utc_date(:,1))    ; use sprinti 
        day   = tointeger(utc_date(:,2))
        value = sprinti("%0.4i", year) + "/" + sprinti("%0.2i", month) + "/" + sprinti("%0.2i", day) 
      else
        value=field&$dim_field_name$(index)
      end if
      select_list=select_list+" "+arg_name+"="+value
      if rk .eq. 4 then
        new_fld=field(index,:,:,:)     
      else 
        if rk .eq. 3 then
          new_fld=field(index,:,:)
        else 
          if rk .eq. 2 then
            new_fld=field(index,:)
          end if ;rk eq 2
        end if ; rk eq 3
      end if ; rk eq 4
      
    end if ;arg is float
  end if ;arg is integer
  
  return(new_fld)
  
end

;--------------------------------------------------------------------------------------------

undef("field_reduce")
; Reduce rank of field called 'varname' in file 'filename' by adapting to various cases, output field is 2D or 1D
function field_reduce(filename:string, varname:string, ystyle:string, useXY:logical, horizontal:logical, useLatDim:logical, vprofile:logical, extract_list:string, lat_exist:logical, lon_exist:logical, tz_profile:logical)

local ffile, field, Var_DimNames, dime, rank, new_field, fldtmp, index, hor_dim, lat_dim, lon_dim
begin
  
  ; Reading the field 
  if isfilepresent(filename) then
    ffile=addfile(filename,"r")
    if isfilevar(ffile,varname) then 
      if (getfilevartypes(ffile,varname) .eq. "short") then
        field = short2flt( ffile->$varname$ )
      else
        field = ffile->$varname$
      end if
    else
      print("File "+filename+" does not include variable "+varname)
      status_exit(1)
    end if
  else
    print("File "+filename+" cannot be accessed")
    status_exit(2)
  end if

  ; Try to adapt to various cases, using heuristics !
  ; Assume dimensions order is anyway t,z,y,x, but that some may be missing 
  Var_DimNames = getVarDimNames(field)  ; getting dimensions from metadata
  dime=dimsizes(field)
  rank=dimsizes(dime)
  if (rank.gt.4) then 
    print("This script is not tuned for high rank data for field ( rank="+rank+")")
    print(Var_DimNames)
    status_exit(1)
  end if
  
  ; Removes singleton (degenerate) dimensions from field while preserving meta data
  new_field=rm_single_dims(field)
  delete(Var_DimNames)
  Var_DimNames=getVarDimNames(new_field)
  delete(dime)
  dime=dimsizes(new_field)
  delete(rank)
  rank=dimsizes(dime)
  delete(field)
  field=new_field
  delete(new_field)
  
  ; Assume first dimension is time. Select first time slice before further processing
  if (rank .eq. 4) then
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
; NOTE : this should be done only if at least two other dimensions have size > 1, otherwise we
; should plot some Hovmoeller diagram
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ; case where (t,z,y,x) are not degenerated
    if ( (isvar("time")) .and. (.not. ismissing(str_match(Var_DimNames(0),"time"))) ) \
      .or. ( (isvar("time")) .and. iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"standard_name") \
      .and. (field&$Var_DimNames(0)$@standard_name .eq. "time") ) then
    
      fldtmp=data_select("time", time, field, Var_DimNames(0), extract_list)
  
    else
      ; case where no time argument or 'bad' files
      print("Selecting first time step")
      fldtmp=field(0,:,:,:)
    end if

    delete(field)
    field=fldtmp
    delete(fldtmp)
    delete(Var_DimNames)
    Var_DimNames = getVarDimNames(field) 
    delete(dime)
    dime=dimsizes(field)
    rank=dimsizes(dime)
  end if
 
  ; Assume second dimension is depth. Select first level before further processing
  if (rank .eq. 3) then 
    ;print("plotting a field with 2 horizontal dimensions")
    ; case where (t,y,x) or (t,z,y) or (t,z,x) are not degenerated
    if ( (isvar("time")) .and. (.not. ismissing(str_match(Var_DimNames(0),"time"))) ) \
      .or. ( (isvar("time")) .and. iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"standard_name") \
      .and. (field&$Var_DimNames(0)$@standard_name .eq. "time") ) then
        
      fldtmp=data_select("time", time, field, Var_DimNames(0), extract_list)
        
      ; case where (t,z,y) or (t,z,x) are not degenerated
      if (isvar("level")) .and. ( .not. ismissing(str_match(Var_DimNames(1),"dept")) \
        .or. (Var_DimNames(1) .eq. "sigma") .or. (Var_DimNames(1) .eq. "z") .or. (Var_DimNames(1) .eq. "Z") \
        .or. (Var_DimNames(1) .eq. "plev") .or. (Var_DimNames(1) .eq. "lev") .or. (Var_DimNames(1) .eq. "level") \
        .or. (Var_DimNames(1) .eq. "p") .or. (Var_DimNames(1) .eq. "pressure") \
        .or. ( iscoord(field,Var_DimNames(1)) .and. isatt(field&$Var_DimNames(1)$,"axis") \
        .and. ((field&$Var_DimNames(1)$@axis .eq. "z") .or. (field&$Var_DimNames(1)$@axis .eq. "Z")) ) ) then

        delete(field)
        field=fldtmp
        delete(fldtmp)

        fldtmp=data_select("level", level, field, Var_DimNames(1), extract_list)
      end if  

    else
      ; case where (z,y,x) are not degenerated
      if (isvar("level")) .and. ( .not. ismissing(str_match(Var_DimNames(0),"dept")) \
        .or. (Var_DimNames(0) .eq. "sigma") .or. (Var_DimNames(0) .eq. "z") .or. (Var_DimNames(0) .eq. "Z") \
        .or. (Var_DimNames(0) .eq. "plev") .or. (Var_DimNames(0) .eq. "lev") .or. (Var_DimNames(0) .eq. "level") \
        .or. (Var_DimNames(0) .eq. "p") .or. (Var_DimNames(0) .eq. "pressure") \
        .or. ( iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"axis") \
        .and. ((field&$Var_DimNames(0)$@axis .eq. "z") .or. (field&$Var_DimNames(0)$@axis .eq. "Z")) ) ) then

        fldtmp=data_select("level", level, field, Var_DimNames(0), extract_list)
       else
        ; case where no time/level arguments or 'bad' files
        print("Selecting first time or level step")
        fldtmp=field(0,:,:)
      
      end if ; level
    end if ; time

    delete(field)
    field=fldtmp
    delete(fldtmp)
    delete(dime)
    dime=dimsizes(field)
    rank=dimsizes(dime)
 end if ; rk eq 3  

  ; field is now 2D, with one or two horizontal dimension(s). Last dim is assumed to be horizontal
  if (rank .eq. 2) then
    delete(Var_DimNames)
    Var_DimNames=getVarDimNames(field) 
    
    if ( isatt(field,"coordinates") .and. ( (.not. ismissing(str_match(field@coordinates,"lon"))) .or. (.not. ismissing(str_match(field@coordinates,"LON"))) ) \ 
           .and. ( (.not. ismissing(str_match(field@coordinates,"lat"))) .or. (.not. ismissing(str_match(field@coordinates,"LAT"))) ) ) \ 
      .or. ( (.not. ismissing(str_match(Var_DimNames(0),"lat"))) \ 
           .and. (.not. ismissing(str_match(Var_DimNames(1),"lon"))) ) \
      .or. ( (.not. ismissing(str_match(Var_DimNames(0),"LAT"))) \ 
           .and. (.not. ismissing(str_match(Var_DimNames(1),"LON"))) ) \
      .or. ( iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"standard_name") \
           .and. (field&$Var_DimNames(0)$@standard_name .eq. "latitude") \
           .and. iscoord(field,Var_DimNames(1)) .and. isatt(field&$Var_DimNames(1)$,"standard_name") \
           .and. (field&$Var_DimNames(1)$@standard_name .eq. "longitude") ) \
      .or. (  iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"axis") \
           .and. ( (field&$Var_DimNames(0)$@axis .eq. "y") .or. (field&$Var_DimNames(0)$@axis .eq. "Y") ) \
           .and. iscoord(field,Var_DimNames(1)) .and. isatt(field&$Var_DimNames(1)$,"axis") \
           .and. ( (field&$Var_DimNames(1)$@axis .eq. "x") .or. (field&$Var_DimNames(1)$@axis .eq. "X") ) ) then
      horizontal=True 
    end if
    
    ;We have a true cross-section, a horizontal map or a profile (t,z)
    if (ystyle .eq. "log") then useXY=False end if

    if (.not.ismissing(Var_DimNames(0)) .and. (Var_DimNames(0) .eq. "lat")) .or. (isatt(field,"coordinates") .and. ((field@coordinates .eq. "lat lon") .or. (field@coordinates .eq. "lon lat"))) then
      latname="lat"
      lonname="lon"
    end if
    if ( .not. ismissing(str_match(Var_DimNames,"latitude")) .or. (isatt(field,"coordinates") .and. .not. ismissing(str_match(field@coordinates,"latitude"))) ) then
      latname="latitude"
      lonname="longitude"
    end if
    if ( .not. ismissing(str_match(Var_DimNames,"LAT")) .or. (isatt(field,"coordinates") .and. .not. ismissing(str_match(field@coordinates,"LAT"))) ) then
      latname="LAT"
      lonname="LON"
    end if
    if ( ismissing(str_match(Var_DimNames,"lat")) .and. ismissing(str_match(Var_DimNames,"LAT")) ) then
      if ( isatt(field,"coordinates") .and. ( (.not. ismissing(str_match(field@coordinates,"lat"))) .or. (.not. ismissing(str_match(field@coordinates,"LAT"))) ) ) \ 
        .or. ( iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"standard_name") \
             .and. (field&$Var_DimNames(0)$@standard_name .eq. "latitude") ) \
        .or. (  iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"axis") \
             .and. ( (field&$Var_DimNames(0)$@axis .eq. "y") .or. (field&$Var_DimNames(0)$@axis .eq. "Y") ) ) then
        useLatDim=True
        if isfilevar(ffile,"nav_lat") then 
          latname="nav_lat" 
          lonname="nav_lon"
        end if
      end if
    end if
   
    if ( isfilevar(ffile,"nav_lat") .and. isfilepresent("coordinates.nc") ) \ 
      .or. ( isfilevar(ffile,"nav_lat") .and. isfilepresent("mesh_mask.nc") ) then
      
      if isfilepresent("coordinates.nc") then
        coord_file=addfile("coordinates.nc","r")

	if isfilevar(ffile,"gphit") then 
           lat_dim=coord_file->gphit
	   lat_dim_tmp=rm_single_dims(lat_dim)
	   delete(lat_dim)
	   lat_dim=lat_dim_tmp
	   delete(lat_dim_tmp)
	end if
	if isfilevar(ffile,"glamt") then 	
	   lon_dim=coord_file->glamt
           lon_dim_tmp=rm_single_dims(lon_dim)
           delete(lon_dim)
           lon_dim=lon_dim_tmp
           delete(lon_dim_tmp)
	end if

      else

        if isfilepresent("mesh_mask.nc") then
          coord_file=addfile("mesh_mask.nc","r")
	  if isfilevar(ffile,"nav_lat") then lat_dim=coord_file->nav_lat end if
	  if isfilevar(ffile,"nav_lon") then lon_dim=coord_file->nav_lon end if
        end if

      end if
    else
      if isfilevar(ffile,"nav_lat") then
        print("File coordinates.nc or mesh_mask.nc cannot be accessed. We will use data file values.")
      end if

      if ( isvar("latname") .and. ( .not. ismissing(str_match(Var_DimNames,latname)) .or. ( isatt(field,"coordinates") .and. (.not. ismissing(str_match(field@coordinates,latname))) ) ) ) then lat_dim=ffile->$latname$ end if
      if ( isvar("lonname") .and. ( .not. ismissing(str_match(Var_DimNames,lonname)) .or. ( isatt(field,"coordinates") .and. (.not. ismissing(str_match(field@coordinates,lonname))) ) ) ) then lon_dim=ffile->$lonname$ end if	
    end if

  end if ; rk eq 2

  if (rank .eq. 1) then 
    delete(Var_DimNames)
    Var_DimNames=getVarDimNames(field)  ; getting dimensions from metadata
    hor_dim=field&$Var_DimNames(0)$
    vprofile=False
    useXY=True
    if ( .not. ismissing(str_match(Var_DimNames(0),"dept")) .or. (Var_DimNames(0) .eq. "sigma") \
      .or. (Var_DimNames(0) .eq. "z") .or. (Var_DimNames(0) .eq. "Z") \
      .or. (Var_DimNames(0) .eq. "plev") .or. (Var_DimNames(0) .eq. "lev") .or. (Var_DimNames(0) .eq. "level") \
      .or. (Var_DimNames(0) .eq. "p") .or. (Var_DimNames(0) .eq. "pressure") \
      .or. ( iscoord(field,Var_DimNames(0)) .and. isatt(field&$Var_DimNames(0)$,"axis") .and. ( (field&$Var_DimNames(0)$@axis .eq. "z") .or. (field&$Var_DimNames(0)$@axis .eq. "Z") ) ) ) then
      print("We have a vertical profile")
      vprofile=True
    end if
  end if

  lat_exist=True
  lon_exist=True
  ; Return depending on rank=1 or rank=2 (and depend on existence of lat and lon variables)
  if (rank .eq. 1) then 
    return([/field, hor_dim/])
  else ;rk eq 2
    if isvar("lon_dim") .and. isvar("lat_dim") then  return([/field, lat_dim, lon_dim/]) end if
    if isvar("lon_dim") .and. .not. isvar("lat_dim") then  
       lat_exist=False
       return([/field, lon_dim/]) 
    end if
    if .not. isvar("lon_dim") .and. isvar("lat_dim") then  
       lon_exist=False
       return([/field, lat_dim/]) 
    end if	 
    if .not. isvar("lon_dim") .and. .not. isvar("lat_dim") then  
      tz_profile=True
      lat_exist=False
      lon_exist=False
      return([/field/])
    end if
  end if
  
end

;--------------------------------------------------------------------------------------------


begin

  if (.not. isvar("cmap")) then cmap="BlueDarkRed18" end if
  if (.not. isvar("myscale")) then myscale=1.0 end if
  if (.not. isvar("myoffset")) then myoffset=0.0 end if
  if (.not. isvar("type")) then type="png" end if
  if (.not. isvar("trim")) then trim=True end if
  LABELON=False
  RASTER=True 

  ;---------------------------------------------------------
  ; Evaluate number of fields and if drawing vectors or not
  ;---------------------------------------------------------

  AUX_FIELD=False
  if (isvar("aux_file")) then
    if (aux_file .ne. "") then  
      AUX_FIELD=True
    end if
  end if

  VECT=False
  if (isvar("u_file") .and. isvar("v_file")) then  
    if (u_file .ne. "" .and. v_file .ne. "") then
      VECT=True
    end if
  end if
  
  ;-----------------
  ; Main field: fld
  ;-----------------
  
  ; Setting variables depending on optional arguments
  if .not. isvar("y") then y="lin" end if ; by default, vertical cross-sections (and profiles) in pressure coordinates will have a data-linear scale

  LEVELS_CONTOURS=False ; by default, contours lines (if actived i.e. if LINEON is True) follow color filled contours
  LINEON=False
  if .not. AUX_FIELD then
    if (isvar("contours") .and. (contours .ne. 0)) then LINEON=True end if
  end if

  if isvar("focus") then
    if (.not. (focus .eq. "" .or. focus .eq. "land" .or. focus .eq. "ocean")) then
      print("Wrong focus option '"+focus+"' . Only 'ocean' and 'land' are allowed")
      status_exit(2)
    end if
  else
    focus=""
  end if   

  print("---------------")
  print("  MAIN FIELD  ")
  print("---------------")

  useXY=False
  horizontal=False
  useLatDim=False
  vprofile=False
  lat_exist=True  
  lon_exist=True
  tz_profile=False

  ; Reading main field and reduction if necessary
  main_field_select=""
  array=field_reduce(main_file, main_var, y, useXY, horizontal, useLatDim, vprofile, main_field_select, lat_exist, lon_exist, tz_profile)
  
  fld=array[0]
  if (ListCount(array) .eq. 2) .and. useXY then ; rk eq 1
    hdim=array[1]
  else 
    if (ListCount(array) .ge. 2) then ; ListCount eq 2 or 3 <=> rk eq 2
      if lat_exist .and. lon_exist then 
        latdim=array[1]
        londim=array[2]
      end if
      if (lat_exist .and. .not. lon_exist) then latdim=array[1] end if
      if (lon_exist .and. .not. lat_exist) then londim=array[1] end if
    end if
  end if
 
  ; Reading main field unit, scaling the field and setting REDU
  if (.not. isvar("units")) then 
    if (isatt(fld,"units")) then units=fld@units else units="None" end if
  end if
  
  units=units 
  fld=fld*tofloat(myscale)+tofloat(myoffset)
  
  controlMinMax= isvar("vmin") .and. isvar("vmax") .and. isvar("vdelta")
  if ( controlMinMax ) then      
    power10=floattoint(log10(dim_max(ndtooned(fld))))
    if power10.ge.4 then
      fld=fld/10^power10
      vmin=tofloat(vmin/10^power10)
      vmax=tofloat(vmax/10^power10)
      vdelta=tofloat(vdelta/10^power10)
    end if
  else
    power10=0
  end if
  
  
  ;-----------------------
  ; Auxiliary field: fld2
  ;-----------------------

  if (AUX_FIELD) then
    
    print("---------------------")
    print("   AUXILIARY FIELD   ")
    print("---------------------")

    useLatDim2=False
    
    useXY_dummy=False
    horizontal_dummy=False
    vprofile_dummy=False
    lat_exist_dummy=True  
    lon_exist_dummy=True
    tz_profile_dummy=False

    ; Reading auxiliary field and reduction if necessary
    aux_field_select=""
    array2=field_reduce(aux_file, aux_var, y, useXY_dummy, horizontal_dummy, useLatDim2, vprofile_dummy, aux_field_select, lat_exist_dummy, lon_exist_dummy, tz_profile_dummy)
      
    fld2=array2[0]
    if (ListCount(array2) .eq. 2) .and. useXY_dummy then
      hdim2=array2[1]
    else 
      if (ListCount(array) .ge. 2) then ; ListCount eq 2 or 3
        if lat_exist_dummy .and. lon_exist_dummy then	
          latdim2=array2[1]	    
          londim2=array2[2]
        end if
        if (lat_exist_dummy .and. .not. lon_exist_dummy) then latdim2=array2[1] end if
        if (lon_exist_dummy .and. .not. lat_exist_dummy) then londim2=array2[1] end if
      end if
    end if
  
  end if ; AUX_FIELD is True

  ;-----------------------
  ; Vectors : fld3 & fld4
  ;-----------------------
 
  if VECT then

    print("---------------------")
    print("    VECTOR FIELDS    ")
    print("---------------------")
    ; fld3 <=> u <=> X, fld4 <=> v <=> Y

    print("-----> Vector u ")

    ; Reading vector u and reduction if necessary
    useXY_dummy=False
    horizontal_dummy=False
    vprofile_dummy=False
    lat_exist_dummy=True
    lon_exist_dummy=True
    tz_profile_dummy=False

    useLatDimv=False
    uselect=""
    array3=field_reduce(u_file, u_var, y, useXY_dummy, horizontal_dummy, useLatDimv, vprofile_dummy, uselect, lat_exist_dummy, lon_exist_dummy, tz_profile_dummy)
      
    fld3=array3[0]
    if (ListCount(array3) .eq. 2) .and. useXY_dummy then
      print("Rank data for vector u is 1 !!! Vectors fields must have a rank >= 2 !")
      status_exit(1)
    else 
      if (ListCount(array) .ge. 2) then ; ListCount eq 2 or 3
        ;latdimv=array3[1]
        if (lat_exist_dummy .and. lon_exist_dummy) then londimv=array3[2] end if
        if (lon_exist_dummy .and. .not. lat_exist_dummy) then londimv=array3[1] end if
      end if
    end if

    print("-----> Vector v ")
    
    ; Reading vector v and reduction if necessary
    useLatDimv=False
    vselect=""
    array4=field_reduce(v_file, v_var, y, useXY_dummy, horizontal_dummy, useLatDimv, vprofile_dummy, vselect, lat_exist_dummy, lon_exist_dummy, tz_profile_dummy)

    fld4=array4[0]
    if (ListCount(array4) .eq. 2) .and. useXY_dummy then
      print("Rank data for vector v is 1 !!! Vectors fields must have a rank >= 2 !")
      status_exit(1)
    else 
      if (ListCount(array) .ge. 2) then ; ListCount eq 3
        ;londimv=array4[2]
        if lat_exist_dummy then latdimv=array4[1] end if
      end if
    end if
     
    ; Rotation des champs de la grille du modele NEMO -> GEO si le fichier des champs d angles est present 
    ; Ex. tests: fld3(:,:)=0.,fld4(:,:)=0.01
    if ( isvar("rotation") .and. (rotation .eq. 1) ) then  
      if isfilepresent("angles.nc") then
        angle_file=addfile("angles.nc","r")
        gcosu=angle_file->GCOSU
        gsinu=angle_file->GSINU
        gsinv=angle_file->GSINV
        gcosv=angle_file->GCOSV
        ; Remove singleton (degenerate) dimensions: (t,y,x) -> (y,x)
        new_gcosu=rm_single_dims(gcosu)
        new_gsinu=rm_single_dims(gsinu)
        new_gcosv=rm_single_dims(gcosv)
        new_gsinv=rm_single_dims(gsinv)
        ; Rotation
        x=fld3
        y=fld4
        fld3 = x * new_gcosu - y * new_gsinu
        fld4 = x * new_gsinv + y * new_gcosv
      else
        print("File angles.nc cannot be accessed")
        status_exit(2)
      end if
    end if 
      
  end if ; VECT

  ;-----------------
  ; Open workstation
  ;-----------------

  if isvar("resolution") then
    if ( .not. ismissing(str_match(resolution,"x")) .or. .not. ismissing(str_match(resolution,"*")) ) then

      if .not. ismissing(str_match(resolution,"x")) then
        nb_pixels_or_inches = str_split(resolution, "x")
      else
        if .not. ismissing(str_match(resolution,"*")) then
          nb_pixels_or_inches = str_split(resolution, "*")
        end if
      end if
      width = stringtofloat(nb_pixels_or_inches(0))
      height = stringtofloat(nb_pixels_or_inches(1))
    
      if type .eq. "png" then ;image resolution in pixels
        type@wkWidth = width
        type@wkHeight = height
      else 
        if (type .eq. "pdf") .or. (type .eq. "eps") then ;image resolution in inches
          type@wkPaperWidthF  = width 
          type@wkPaperHeightF = height
        end if
      end if

    else ; resolution is not given by 'widthxheight' or 'width*height'
      
      if type .eq. "png" then 
        print("For PNG output, resolution must be given by 'widthxheight' or 'width*height'")
        status_exit(1)
      else
        if (type .eq. "pdf") .or. (type .eq. "eps") then
          type@wkPaperSize = resolution
        end if
      end if

    end if ; str_match
  end if ;resolution
  ;if type .eq. "pdf" then
  ;  type@wkPDFResolution = 3600
  ;end if
  wks = gsn_open_wks(type,plotname)
  gsn_define_colormap(wks,cmap) 
  i = NhlNewColor(wks,0.8,0.8,0.8)
  ; reverse colormap
  if (isvar("reverse") .and. reverse) then gsn_reverse_colormap(wks) end if

  ; main field resources
  res=True
  res@gsnFrame= False
  res@gsnDraw= False
  res@gsnMaximize = True
  res@gsnPaperOrientation="portrait" 

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; ContourPlot resources
  if (.not. useXY  ) then 
    res@gsnSpreadColors     = True
    res@gsnSpreadColorEnd   = -2

    ; Main title
    if (.not. isvar("title")) then title=main_var end if 
    res@tiMainString  = title

    if isvar("tiMainFont") then
      res@tiMainFont = tiMainFont ;22 <=> "helvetica-bold"
    end if
    if isvar("tiMainFontHeightF") then
      res@tiMainFontHeightF  = tiMainFontHeightF ;.030       
    end if
    if isvar("tiMainPosition") then
      res@tiMainPosition = tiMainPosition
      res@tiMainJust = "Center"+tiMainPosition
    end if

    ; 3 sub-titles (label, font, height)
    if isvar("gsnLeftString") then
      res@gsnLeftString = gsnLeftString
    end if
    
    if .not. isvar("gsnRightString") then
      if power10.ge.4 then
        res@gsnRightString = units+" x10~S~"+sprinti("%0.1i", power10)+""
      else
        res@gsnRightString = units ; ncl default
      end if
    else
      res@gsnRightString = gsnRightString
    end if

    if .not. isvar("gsnCenterString") then
      if ( isvar("time") .or. isvar("level") ) then
        main_field_select=str_left_strip(main_field_select) ; strips leading spaces from the input strings
        main_field_select=str_sub_str(main_field_select," ",", ") 
        main_field_select=str_sub_str(main_field_select,"time","t")
        main_field_select=str_sub_str(main_field_select,"level","l")
        res@gsnCenterString = main_field_select     
      end if
    else 
      res@gsnCenterString = gsnCenterString
    end if
    
    if isvar("gsnStringFont") then
      res@gsnStringFont = gsnStringFont ; "helvetica-bold"
    end if
    if isvar("gsnStringFontHeightF") then
      res@gsnStringFontHeightF = gsnStringFontHeightF
    else 
      res@gsnStringFontHeightF = 0.012
    end if

    ; Label Bar
    res@lbLabelBarOn = True
    res@lbTitleOn    = False
    ;res@lbTitleFontHeightF=0.012
    
    if isvar("lbLabelFontHeightF") then
      res@lbLabelFontHeightF=lbLabelFontHeightF ;0.012
    end if
    res@lbLabelFontThicknessF= 3.0
    res@lbTitleFontThicknessF= 3.0
    res@lbLabelAutoStride = True
    if ( .not. isvar("vcb") .or. (isvar("vcb") .and. vcb) ) then
      res@lbOrientation     = "vertical"
    else
      if ( isvar("vcb") .and. (.not. vcb) ) then
        res@lbOrientation     = "horizontal"
      end if
    end if

    ; Axis Label 
    if (.not. isvar("proj")) then 
      if isvar("tmYLLabelFontHeightF") then
        res@tmYLLabelFontHeightF = tmYLLabelFontHeightF ;0.015
      end if
      if isvar("tmXBLabelFontHeightF") then
        res@tmXBLabelFontHeightF = tmXBLabelFontHeightF ;0.015
      end if
      
      if type.eq."png" then
        res@tmYLLabelFontThicknessF=2.0
        res@tmXBLabelFontThicknessF=2.0
      end if
     
    else ; proj
      if isvar("gsnPolarLabelFontHeightF") then
        res@gsnPolarLabelFontHeightF=gsnPolarLabelFontHeightF
      end if
    end if

    if (.not. horizontal) then ; cross-section
      if isvar("tmYRLabelFontHeightF") then
        res@tmYRLabelFontHeightF=tmYRLabelFontHeightF
      end if
    end if
    
    if isvar("tiYAxisFontHeightF") then
      res@tiYAxisFontHeightF=tiYAxisFontHeightF
    else
      if (.not. horizontal) then ; cross-section
        res@tiYAxisFontHeightF=0.024
      end if
    end if
    
    if isvar("tiXAxisFontHeightF") then
      res@tiXAxisFontHeightF=tiXAxisFontHeightF
    end if
      
    ; Levels
    res@cnFillOn=True 
    if (controlMinMax) then 
      res@cnLevelSelectionMode = "ManualLevels" 
      res@cnMinLevelValF = vmin
      res@cnMaxLevelValF = vmax
      res@cnLevelSpacingF = vdelta
    end if
    if ( isvar("colors") ) then
      res@cnLevelSelectionMode = "ExplicitLevels"
      res@cnLevels=tofloat(str_split(colors," "))
      delete(colors)
    end if 

    res@cnLinesOn=False
    res@cnLineLabelsOn = False
    res@cnLineLabelBackgroundColor = "white"
    res@cnInfoLabelString =""

    if .not. AUX_FIELD then
      if (LINEON) then 
        if (contours .eq. 1) then 
          res@cnLinesOn=True
          res@gsnContourNegLineDashPattern = 1
          res@gsnContourZeroLineThicknessF = 1.5  
        else
          if (isstring(contours)) then
            LEVELS_CONTOURS=True
            res2=True
            res2@gsnFrame= False
            res2@gsnDraw=False
            res2@gsnMaximize = True
            res2@gsnLeftString     = ""
            res2@gsnRightString    = ""
            res2@gsnCenterString   = ""

            res2@cnFillOn=False
            res2@cnLineLabelsOn = False
            res2@cnInfoLabelString = ""

            res2@cnLinesOn=True
            res2@gsnContourNegLineDashPattern = 1
            res2@gsnContourZeroLineThicknessF = 1.5  
            
            res2@cnLevelSelectionMode = "ExplicitLevels"
            res2@cnLevels=tofloat(str_split(contours," "))
            delete(contours)

            if (horizontal) then
              res2@gsnAddCyclic = False
              if useLatDim then
                if isvar("londim") then res2@sfXArray = londim end if
                if isvar("latdim") then res2@sfYArray = latdim end if
                res2@gsnAddCyclic        = False
              end if
            end if

          end if 
        end if
      end if   
    else
      ; auxiliary field
      res2=True
      res2@gsnFrame= False
      res2@gsnDraw=False
      res2@gsnMaximize = True
      
      res2@gsnLeftString     = ""
      res2@gsnRightString    = ""
      res2@gsnCenterString   = ""
      
      res2@cnFillOn=False 
      res2@cnLineLabelsOn = False
      res2@cnLineLabelBackgroundColor = -1
      res2@cnInfoLabelString = ""
    
      res2@cnLinesOn=True 
      res2@gsnContourNegLineDashPattern = 1
      res2@gsnContourZeroLineThicknessF = 1.5  
      
      if ( isvar("contours") ) then 
        res2@cnLevelSelectionMode = "ExplicitLevels"
        res2@cnLevels=tofloat(str_split(contours," "))
        delete(contours)     
      end if 

      if (horizontal) then
        res2@gsnAddCyclic = False
        if useLatDim2 then
          if isvar("londim2") then res2@sfXArray = londim2 end if
          if isvar("latdim2") then res2@sfYArray = latdim2 end if
          res2@gsnAddCyclic        = False
        end if
      end if
     
      if ( isvar("shade_below") .or. isvar("shade_above") ) then 
        ; without low or high values, for shading all the field
        ;res2@cnMonoFillPattern = True
        ;res2@cnMonoFillColor   = True   ; Use same fill color
        ;res2@cnFillPattern = 3

        res2@lbLabelBarOn = False
        res2@cnFillOn=True
        ;res2@cnFillColor    = "black"   ; foreground by default
        opt = True
        opt@gsnShadeFillType = "pattern" ; "color" or "pattern"
      end if
      
    end if ; NOT AUX_FIELD

    if (LABELON) then 
      res@cnLineLabelsOn=True
    end if   
    if (RASTER) then
      if (.not. LINEON) then 
        res@cnLinesOn=False
        res@cnLineLabelsOn=False
      end if
      ;res@cnRasterModeOn=True
      res@cnFillMode = "RasterFill" 
      res@cnRasterSmoothingOn=True 
    end if

    if (horizontal) then ; only for map
      res@mpFillColors = (/-1,-1,-1,-1/)
      res@mpPerimOn = False
      res@mpGeophysicalLineThicknessF = 2
      res@mpFillOn=False
      res@mpFillDrawOrder="PostDraw"
      if (focus .eq. "land")  then 
        print("Greying out oceans")
        res@mpFillOn=True
        res@mpLandFillColor=-1
        res@mpInlandWaterFillColor = -1 
        res@mpOceanFillColor="grey" 
      end if
      if (focus .eq. "ocean") then 
        res@mpFillOn=True
        res@mpLandFillColor="grey" 
      end if
      
      if (isvar("proj")) then 
        prj = getSubString( proj,0,1 )
        if ( prj .eq. "NH" .or. prj .eq. "SH" ) then
          res@gsnPolar=prj
          lenproj = strlen(proj)
          minlat = 50.
          if ( lenproj .ge. 3 ) then minlat = tofloat( getSubString(proj,2,lenproj-1) ) end if
          if ( prj .eq. "SH" ) then
            res@mpMaxLatF=-minlat
            ;delete(res@mpMinLatF)
          end if
          if ( prj .eq. "NH" ) then
            res@mpMinLatF=minlat
            ;delete(res@mpMaxLatF)
          end if 
        end if
      else
        res@mpMinLatF                = min(latdim)
        res@mpMaxLatF                = max(latdim)
        if (isvar("mpCenterLonF")) then
          do while ((mpCenterLonF .gt. 180) .and. (mpCenterLonF .le. 360))
            mpCenterLonF=mpCenterLonF-360
          end do
          if mpCenterLonF .gt. (max(londim)-180) then
            print("out of data range: mpCenterLonF + 180 > max(londim)")
          end if
          if mpCenterLonF .lt. (max(londim)-180) then
            ; pivots about a user-specified longitude (rectilinear grids only)
            fld_flip=lonPivot(fld,mpCenterLonF+180) 
            delete(fld)
            fld=fld_flip
            delete(fld_flip)
          end if
          res@mpCenterLonF=mpCenterLonF
        else
          res@mpMinLonF                = min(londim)
          res@mpMaxLonF                = max(londim)
          res@mpCenterLonF=(min(londim)+max(londim))/2
        end if

      end if
    end if ;end horizontal
    
    if VECT then 
      if (isvar("vcRefMagnitudeF")) then                ; define vector ref magnitude
        res@vcRefMagnitudeF=vcRefMagnitudeF
      end if
    
      if (isvar("vcRefLengthF")) then         	        ; define size of ref vector
        res@vcRefLengthF=vcRefLengthF
      end if
      
      if (isvar("vcMinDistanceF")) then                 ; define density of vectors
        res@vcMinDistanceF        = vcMinDistanceF
      else
        res@vcMinDistanceF        = 0.017               ; thins arrows near pole
      end if
      
      if (isvar("vcGlyphStyle")) then                   ; style of vectors: "LineArrow" (default), "CurlyVector", ...
        res@vcGlyphStyle          = vcGlyphStyle
      end if
      
      res@vcRefAnnoOrthogonalPosF = -1.                 ; move ref vector  
      res@vcRefAnnoParallelPosF = 1.                    ; move ref vector  
      res@vcRefAnnoFontHeightF = 0.01

      res@vcMonoLineArrowColor  =  True                 ; vectors color by magnitude
      if (isvar("vcLineArrowColor")) then               ; change vector color
        res@vcLineArrowColor      =  vcLineArrowColor
      else
        res@vcLineArrowColor      = "white"            
      end if
      
      res@vcLineArrowThicknessF   = 2                   ; change vector thickness
      res@gsnScalarContour=True                         ; contours desired
      res@vcVectorDrawOrder       = "PostDraw"          ; draw vectors last  
      
      if (horizontal) then 
        res@gsnAddCyclic = False
        if useLatDimv then
          res@vfXArray            = londimv
          res@vfYArray            = latdimv
          res@gsnAddCyclic        = False
        end if
      end if
    end if ; VECT
    
  end if  ; not useXY
  
  ; Viewport resources
  res@vpWidthF  =0.8
  res@vpHeightF = 0.8

  res@vpXF      =0.1
  res@vpYF      =0.96

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
 
  ; Profile
  if useXY then 
    xyres = True
    xyres@gsnDraw = False
    xyres@gsnFrame = False
    xyres@gsnMaximize = True
    xyres@gsnPaperOrientation = "portrait"
    xyres@vpXF = 0.5
    xyres@vpWidthF = 0.45
    xyres@vpHeightF = 0.45
    xyres@txFontHeightF = 0.010
    xyres@xyLineThicknesses = 3.0 ;(/ 2.0, 3.0/)
    xyres@xyLineColor = "black"
    xyres@tiMainString       = title
    if isatt(fld,"long_name") then xyres@gsnLeftString=fld@long_name end if
    xyres@gsnRightString=units 
    if isvar("tiMainFontHeightF") then
      xyres@tiMainFontHeightF=tiMainFontHeightF
    end if
    if isvar("tmXBLabelFontHeightF") then
      xyres@tmXBLabelFontHeightF=tmXBLabelFontHeightF 
    else
      xyres@tmXBLabelFontHeightF=0.008
    end if
    if isvar("tmYLLabelFontHeightF") then
      xyres@tmYLLabelFontHeightF=tmYLLabelFontHeightF 
    else
      xyres@tmYLLabelFontHeightF=0.008
    end if
    if isvar("tiXAxisFontHeightF") then
      xyres@tiXAxisFontHeightF=tiXAxisFontHeightF
    end if
    if isvar("tiYAxisFontHeightF") then
      xyres@tiYAxisFontHeightF=tiYAxisFontHeightF
    end if
   
    ; auxiliary field
    if AUX_FIELD then
      xyres2 = True
      xyres2@gsnDraw = False
      xyres2@gsnFrame = False
      xyres2@xyLineThicknesses = 3.0
    end if
    
    if type.eq."png" then
      xyres@tmXBLabelFontThicknessF = 3.0
      xyres@tmYLLabelFontThicknessF = 3.0
      xyres@txFontThicknessF = 3.0
    end if
    
    if vprofile then
      if (isvar("invXY") .and. invXY) then
        xyres@trXReverse = True ; reverse X-axis
        xaxis=hdim
        yaxis=fld
        if (y .eq. "log") then
          xyres@xyXStyle = "Log" 
        else
          if (y .eq. "index") then  ; index linear 
            xyres@xyXStyle = "Irregular"
            xyres@xyXIrregularPoints = hdim
          end if
        end if
        if isvar("vmin") then xyres@trYMinF=vmin end if
        if isvar("vmax") then xyres@trYMaxF=vmax end if
      else
        xyres@trYReverse = True ; reverse Y-axis
        xaxis=fld
        yaxis=hdim
        if (y .eq. "log") then
          xyres@xyYStyle = "Log" 
          set_pres_hgt_axes(hdim,xyres,True)
        else
          if (y .eq. "index") then  ; index linear 
            xyres@xyYStyle = "Irregular"
            xyres@xyYIrregularPoints = hdim
          end if
        end if
        if isvar("vmin") then xyres@trXMinF=vmin end if
        if isvar("vmax") then xyres@trXMaxF=vmax end if
      end if      
      if isvar("options") then res_list(options, xyres) end if
      plot0=gsn_csm_xy(wks,xaxis,yaxis,xyres)
      if AUX_FIELD then
        if (isvar("invXY") .and. invXY) then
          xaxis2=hdim2
          yaxis2=fld2
        else
          xaxis2=fld2
          yaxis2=hdim2
        end if
        if isvar("aux_options") then res_list(aux_options, xyres2) end if 
        plot1=gsn_csm_xy(wks,xaxis2,yaxis2,xyres2)
      end if

    else
      if (isvar("invXY") .and. invXY) then
        xaxis=fld
        yaxis=hdim
        if isvar("vmin") then xyres@trXMinF=vmin end if
        if isvar("vmax") then xyres@trXMaxF=vmax end if
      else
        xaxis=hdim
        yaxis=fld
        if isvar("vmin") then xyres@trYMinF=vmin end if
        if isvar("vmax") then xyres@trYMaxF=vmax end if
      end if
      if isvar("options") then res_list(options, xyres) end if 
      plot0=gsn_csm_xy(wks,xaxis,yaxis,xyres)
      if AUX_FIELD then
        if (isvar("invXY") .and. invXY) then
          xaxis2=fld2
          yaxis2=hdim2
        else
          xaxis2=hdim2
          yaxis2=fld2
        end if
        if isvar("aux_options") then res_list(aux_options, xyres2) end if 
        plot1=gsn_csm_xy(wks,xaxis2,yaxis2,xyres2)
      end if
      
    end if
  else
    if (horizontal) then 
      res@gsnAddCyclic = False
      if useLatDim then 
        ;res@trGridType          = "TriangularMesh"
        res@sfXArray            = londim
        res@sfYArray            = latdim
        res@gsnAddCyclic        = False
      end if
     
      if (isvar("proj")) then 
        
        print("using gsn_csm_contour_map")
        if isvar("options") then res_list(options, res) end if 
        if VECT then
          plot0=gsn_csm_vector_scalar_map(wks,fld3,fld4,fld,res)
        else
          plot0=gsn_csm_contour_map(wks,fld,res)
        end if
        
        if .not. AUX_FIELD then
          if (LEVELS_CONTOURS) then
            if isvar("options") then res_list(options, res2) end if 
            plot1=gsn_csm_contour(wks,fld,res2)
          end if
        else
          if isvar("aux_options") then res_list(aux_options, res2) end if 
          plot1=gsn_csm_contour(wks,fld2,res2)
          if ( isvar("shade_below") .or. isvar("shade_above") ) then plot1 = shading(plot1,fld2,opt) end if
        end if
        ;if VECT then
        ;  plot2=gsn_csm_vector(wks,fld3,fld4,res)
        ;end if
        
      else 
        print("using gsn_csm_contour_map_ce")
        if isvar("options") then res_list(options, res) end if 
        if VECT then
          plot0=gsn_csm_vector_scalar_map_ce(wks,fld3,fld4,fld,res)
        else
          plot0=gsn_csm_contour_map_ce(wks,fld,res)
        end if

        if .not. AUX_FIELD then
          if (LEVELS_CONTOURS) then
            if isvar("options") then res_list(options, res2) end if 
            plot1=gsn_csm_contour(wks,fld,res2)
          end if
        else
          if isvar("aux_options") then res_list(aux_options, res2) end if 
          plot1=gsn_csm_contour(wks,fld2,res2)
	  if ( isvar("shade_below") .or. isvar("shade_above") ) then plot1 = shading(plot1,fld2,opt) end if
        end if
        ;if VECT then
        ;  plot2=gsn_csm_vector(wks,fld3,fld4,res)
        ;end if

      end if 
    else ; cross-section or (t,z) profile
      
      if (y .eq. "log") then

        print("using gsn_csm_pres_hgt")
        if tz_profile .and. (isvar("invXY") .and. invXY) then  
          VarDim=getVarDimNames(fld) 
          tr_fld=fld($VarDim(1)$|:,$VarDim(0)$|:)
          delete(fld)
          fld=tr_fld
          delete(tr_fld)
        end if
        if isvar("options") then res_list(options, res) end if 
        plot0=gsn_csm_pres_hgt(wks,fld,res)
        if .not. AUX_FIELD then
          if(LEVELS_CONTOURS) then
            res2@tmYRMode = "Automatic" ; turn off special labels on right axis
            if isvar("options") then res_list(options, res2) end if 
            plot1=gsn_csm_pres_hgt(wks,fld,res2)
          end if
        else
          res2@tmYRMode = "Automatic"   ; turn off special labels on right axis
          if tz_profile .and. (isvar("invXY") .and. invXY) then  
            delete(VarDim)
            VarDim=getVarDimNames(fld2) 
            tr_fld2=fld2($VarDim(1)$|:,$VarDim(0)$|:)
            delete(fld2)
            fld2=tr_fld2
            delete(tr_fld2)
          end if 
          if isvar("aux_options") then res_list(aux_options, res2) end if 
          plot1=gsn_csm_pres_hgt(wks,fld2,res2)
          if ( isvar("shade_below") .or. isvar("shade_above") ) then plot1 = shading(plot1,fld2,opt) end if
        end if

      else

        if (y .eq. "index") then

          print("using gsn_csm_contour; y with index-linear spacing")
          ;useless :
          ;res@sfYArray            = zdim
          res@trYReverse        = True ; reverse Y-axis          
          if tz_profile .and. (isvar("invXY") .and. invXY) then  
            VarDim=getVarDimNames(fld) 
            tr_fld=fld($VarDim(1)$|:,$VarDim(0)$|:)
            delete(fld)
            fld=tr_fld
            delete(tr_fld)
          end if
          if isvar("options") then res_list(options, res) end if 
          plot0=gsn_csm_contour(wks,fld,res)
          if .not. AUX_FIELD then
            if(LEVELS_CONTOURS) then
              if isvar("options") then res_list(options, res2) end if 
              plot1=gsn_csm_contour(wks,fld,res2)
            end if
          else
            if tz_profile .and. (isvar("invXY") .and. invXY) then  
              delete(VarDim)
              VarDim=getVarDimNames(fld2) 
              tr_fld2=fld2($VarDim(1)$|:,$VarDim(0)$|:)
              delete(fld2)
              fld2=tr_fld2
              delete(tr_fld2)
            end if  
            if isvar("aux_options") then res_list(aux_options, res2) end if 
            plot1=gsn_csm_contour(wks,fld2,res2)
            if ( isvar("shade_below") .or. isvar("shade_above") ) then plot1 = shading(plot1,fld2,opt) end if
          end if

        else 
       
          if (y .eq. "lin") then
          
            print("using gsn_csm_contour; y with data-linear spacing")
            res@trYReverse        = True ; reverse Y-axis
            res@gsnYAxisIrregular2Linear= True
            if tz_profile .and. (isvar("invXY") .and. invXY) then  
              VarDim=getVarDimNames(fld) 
              tr_fld=fld($VarDim(1)$|:,$VarDim(0)$|:)
              delete(fld)
              fld=tr_fld
              delete(tr_fld)
            end if
            if isvar("options") then res_list(options, res) end if 
            plot0=gsn_csm_contour(wks,fld,res)
            if .not. AUX_FIELD then
              if(LEVELS_CONTOURS) then
                res2@gsnYAxisIrregular2Linear= True
                if isvar("options") then res_list(options, res2) end if 
                plot1=gsn_csm_contour(wks,fld,res2)
              end if
            else              
              res2@gsnYAxisIrregular2Linear= True
              if tz_profile .and. (isvar("invXY") .and. invXY) then  
                delete(VarDim)
                VarDim=getVarDimNames(fld2) 
                tr_fld2=fld2($VarDim(1)$|:,$VarDim(0)$|:)
                delete(fld2)
                fld2=tr_fld2
                delete(tr_fld2)
              end if 
              if isvar("aux_options") then res_list(aux_options, res2) end if 
              plot1=gsn_csm_contour(wks,fld2,res2)
              if ( isvar("shade_below") .or. isvar("shade_above") ) then plot1 = shading(plot1,fld2,opt) end if
            end if

          end if
        end if

      end if 
    end if
    
  end if

  if LEVELS_CONTOURS .or. AUX_FIELD then
    overlay(plot0,plot1)   
  end if

  ;if VECT then
  ;  overlay(plot0,plot2)  
  ;end if

  draw(plot0)
  
  frame(wks)

  if ( type .eq. "png" .and. trim ) then
      system("convert "+plotname+" -trim "+plotname)
  end if

  status_exit(0)
  
end
